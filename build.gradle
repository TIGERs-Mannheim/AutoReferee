/*
 * Copyright (c) 2009 - 2021, DHBW Mannheim - TIGERs Mannheim
 */

plugins {
    id "com.github.breadmoirai.github-release" version "2.4.1"
    id 'idea'

    id 'sumatra.java'
    id 'sumatra.jib'
    id 'sumatra.application'
}

def gitDescribe = providers.exec {
    commandLine("git", "describe", "--tags", "--always", "--first-parent")
}.standardOutput.asText.map { it.trim() }

version = gitDescribe.get()

dependencies {
    implementation project(':autoreferee-gui')

    implementation project(':common')
    implementation project(':common-gui')
    implementation project(':common-gui-config')

    implementation project(':sumatra-model')
    implementation project(':sumatra-gui-log')
    implementation project(':sumatra-gui-referee')
    implementation project(':sumatra-gui-replay')
    implementation project(':sumatra-gui-visualizer')

    implementation project(':moduli-record')
    implementation project(':moduli-autoreferee')
    runtimeOnly project(':moduli-autoreferee-ci')
    implementation project(':moduli-cam')
    implementation project(':moduli-referee')
    implementation project(':moduli-wp')

    implementation(libs.com.github.TIGERs.mannheim.infonode)

    implementation(libs.org.apache.logging.log4j.log4j.api)
    runtimeOnly(libs.org.apache.logging.log4j.log4j.core)

    implementation(libs.info.picocli.picocli)
    annotationProcessor(libs.info.picocli.picocli.codegen)
}

application {
    mainClass.set('edu.tigers.autoref.AutoReferee')
}

jib {
    to {
        image = "registry.hub.docker.com/tigersmannheim/auto-referee:latest"
        tags = [project.version.toString()]
        auth {
            username = System.getenv("DOCKER_HUB_USERNAME") ?: ""
            password = System.getenv("DOCKER_HUB_PASSWORD") ?: ""
        }
    }
    container {
        workingDirectory = "/"
    }
    extraDirectories {
        paths {
            path {
                from = file('config')
                into = '/config'
            }
            path {
                from = file('src/main/jib')
                into = '/'
            }
        }
    }
}

distributions {
    main {
        contents {
            from('config') {
                into 'config'
            }
        }
    }
}

githubRelease {
    token System.getenv("GITHUB_TOKEN") ?: ""
    owner "TIGERs-Mannheim"
    targetCommitish.set("master")
    repo.set("AutoReferee")
    releaseAssets distZip
    tagName.set(project.version.toString())
}
