/*
 * Copyright (c) 2009 - 2021, DHBW Mannheim - TIGERs Mannheim
 */

plugins {
    id 'sumatra.java'
    id 'application'

    id 'com.palantir.git-version' version '0.13.0'
    id 'ca.cutterslade.analyze' version '1.8.3'
    id 'com.google.cloud.tools.jib' version '3.2.0'
    id "com.github.breadmoirai.github-release" version "2.2.12"
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id "com.google.protobuf" version "0.8.18" apply(false)
}

ext.versionDetails = versionDetails(prefix: "version/")
version gitVersion(prefix: "version/")

tasks.named('wrapper').configure {
    // Use the 'all' distributable when upgrading the Gradle wrapper
    distributionType = Wrapper.DistributionType.ALL
}

dependencies {
    implementation project(':autoreferee-gui')

    implementation project(':common-gui')
    implementation project(':common-gui-config')

    implementation project(':sumatra-model')
    implementation project(':sumatra-gui-log')
    implementation project(':sumatra-gui-referee')
    implementation project(':sumatra-gui-replay')
    implementation project(':sumatra-gui-visualizer')

    implementation project(':moduli-record')
    implementation project(':moduli-autoreferee')
    runtimeOnly project(':moduli-wp')
    runtimeOnly project(':moduli-referee')

    implementation 'com.github.TIGERs-Mannheim:moduli:4.1'
    implementation 'com.github.TIGERs-Mannheim:infonode:1.7.0'

    implementation 'org.apache.logging.log4j:log4j-api:2.17.1'
    runtimeOnly 'org.apache.logging.log4j:log4j-core:2.17.1'
    runtimeOnly 'org.apache.logging.log4j:log4j-1.2-api:2.17.1'
    runtimeOnly 'org.slf4j:slf4j-log4j12:1.7.36'

    implementation 'commons-cli:commons-cli:1.5.0'
}

application {
    mainClass.set('edu.tigers.autoref.AutoReferee')
    applicationDefaultJvmArgs = [
            '-Xms64m',
            '-Xmx2G',
            '-server',
            '-Dsun.java2d.d3d=false',
            '-Djava.net.preferIPv4Stack=true',
    ]
}

jib {
    to {
        image = "registry.hub.docker.com/tigersmannheim/auto-referee:latest"
        auth {
            username = System.getenv("DOCKER_HUB_USERNAME") ?: ""
            password = System.getenv("DOCKER_HUB_PASSWORD") ?: ""
        }
        tags = [rootProject.version]
    }
    from {
        // gcr.io/distroless/java17
        image = "gcr.io/distroless/java17@sha256:91f381a50e6889fb96feee76b108689bf090709730d65ee6463da8c3e3c1f191"
    }
    extraDirectories {
        paths {
            path {
                from = file('config')
                into = '/config'
            }
            path {
                from = file('src/main/jib')
                into = '/'
            }
        }
    }
}

run.workingDir = rootProject.projectDir

// set version here to avoid overriding the project version.
// version is set to null to skip it completely in the artifact names
// Thus, it is guaranteed that old artifacts are always overwritten.
distZip.archiveVersion.convention(null)
jar.archiveVersion.convention(null)

// disable tar distribution to avoid redundant artifacts
distTar.enabled = false

distributions {
    main {
        contents {
            from('config') {
                into 'config'
            }
        }
    }
}

githubRelease {
    token System.getenv("GITHUB_TOKEN") ?: ""
    owner "TIGERs-Mannheim"
    repo.set("AutoReferee")
    releaseAssets distZip
    tagName { "version-${versionDetails.lastTag}" }
}
